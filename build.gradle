import net.minecraftforge.artifactural.buildscript.JarTransformationTask
import net.minecraftforge.artifactural.buildscript.SoftwareComponentFactoryGrabber
import org.objectweb.asm.Label
import org.objectweb.asm.Opcodes
import org.objectweb.asm.tree.*

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.minecraftforge.gradleutils' version '[2.3,2.4]'
    id 'net.minecraftforge.licenser' version '1.2.0'
    //id 'dev.gradleplugins.gradle-plugin-development' version '1.9.0'
}

group = 'top.outlands'
version = gradleutils.tagOffsetVersion
println "Version: $version"
println "Java: ${System.getProperty 'java.version'} JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}) Arch: ${System.getProperty 'os.arch'}"

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

sourceSets {
    api
    shared
    gradlecomp
}

configurations {
    sharedImplementation.extendsFrom apiImplementation
    gradlecompImplementation.extendsFrom sharedImplementation

    implementation.extendsFrom sharedImplementation
    implementation.extendsFrom gradlecompImplementation
}

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
}

dependencies {
    sharedImplementation sourceSets.api.output

    gradlecompImplementation sourceSets.shared.output
    gradlecompImplementation gradleApi()
    gradlecompImplementation 'com.google.guava:guava:33.5.0-jre'
    gradlecompImplementation 'net.minecraftforge:unsafe:0.2.0'

    implementation sourceSets.api.output
    implementation sourceSets.shared.output
    implementation sourceSets.gradlecomp.output
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'utf-8'
    options.deprecation = true
}



tasks.named('jar', Jar).configure {
    from sourceSets.api.output
    from sourceSets.shared.output
    from sourceSets.gradlecomp.output
}

tasks.named('sourcesJar', Jar).configure {
    from sourceSets.api.allSource
    from sourceSets.shared.allSource
    from sourceSets.gradlecomp.allSource
}

license {
    header = file("$rootDir/header.txt")
}



configurations.sourcesElements.outgoing {
    it.artifacts.removeIf {
        it.classifier != 'sources'
    }
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java

        pom {
            groupId = project.group
            version = project.version
            artifactId = 'artifactural'
            packaging = 'jar'
            name = 'Artifactural'
            description = 'A Gradle artifact processing and management tool'
            url = 'https://github.com/kappa-maintainer/Artifactural/'

            gradleutils.pom.setGitHubDetails(pom, 'Artifactural')

            license gradleutils.pom.Licenses.LGPLv2_1
        }
    }

    repositories {
        maven {
            name = "outlandsReleases"
            url = "https://maven.outlands.top/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }

}

changelog {
    from '3.0'
}
